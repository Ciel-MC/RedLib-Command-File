{
    parserClass="com.github.shuaiouke.redlibcommandfile.language.parser.RedLibCommandParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="RedLibCommand"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.shuaiouke.redlibcommandfile.language.psi"
    psiImplPackage="com.github.shuaiouke.redlibcommandfile.language.psi.impl"

    elementTypeHolderClass="com.github.shuaiouke.redlibcommandfile.language.psi.RedLibCommandTypes"
    elementTypeClass="com.github.shuaiouke.redlibcommandfile.language.psi.RedLibCommandElementType"
    tokenTypeClass="com.github.shuaiouke.redlibcommandfile.language.psi.RedLibCommandTokenType"

}

root ::= root_item*

private root_item ::= (CommandDef_Base) | COMMENT

private CommandDef_Base ::= CommandDefinition_Base | CommandDefinition_Consuming_Base

private CommandDef ::= CommandDefinition | CommandDefinition_Consuming | newline

private CommandDef_Consumed ::= CommandDefinition_Consumed | newline

private CommandDefinition ::= SPACE* commandname (aliasseparator alias)* (SPACE Args)? newline? Open_Bracket (Line | Blank_Line)* Close_Bracket

private CommandDefinition_Consuming ::= SPACE* commandname (aliasseparator alias)* (SPACE Args_Can_Consume)? newline? Open_Bracket (Line_Consumed | Blank_Line)* Close_Bracket

private CommandDefinition_Base ::= SPACE* commandname (aliasseparator alias)* (SPACE Args)? newline? Open_Bracket (Line_Base | Blank_Line)* Close_Bracket

private CommandDefinition_Consuming_Base ::= SPACE* commandname (aliasseparator alias)* (SPACE Args_Can_Consume)? newline? Open_Bracket (Line_Base_Consuming | Blank_Line)* Close_Bracket

private CommandDefinition_Consumed ::= SPACE* commandname (aliasseparator alias)* (SPACE Args_Can_Consume)? newline? Open_Bracket (Line_Consumed_Recovering | Blank_Line)* Close_Bracket

private Line ::= CommandProperty | CommandDef

private Line_Consumed ::= CommandProperty | CommandDef_Consumed

private Line_Consumed_Recovering ::= CommandProperty_Consumed | CommandDef

private Line_Base ::= CommandProperty_Base | CommandDef

private Line_Base_Consuming ::= CommandProperty_Base | CommandDefinition_Consumed

private Blank_Line ::= (SPACE* newline) | COMMENT

private CommandProperty ::= (SPACE)* (HelpLine|PermissionLine|UserLine|HookLine|ContextLine|AssertLine|NoHelpLine|NoTabLine|PostArgLine|HelpMsgLine) newline

private CommandProperty_Consumed ::= (SPACE)* (HelpLine|PermissionLine|UserLine|HookLine|ContextLine|AssertLine|NoHelpLine|NoTabLine|HelpMsgLine) newline

private CommandProperty_Base ::= (SPACE)* (HelpLine|PermissionLine|UserLine|HookLine|ContextLine|AssertLine|NoHelpLine|NoTabLine|HelpMsgLine) newline

//Help is a line for defining a command's subcommand "help"
private HelpLine ::= help separator helpmessage (SPACE helpmessage)* {pin=2}

private HelpMsgLine ::= helpmsg separator helpmsgkey (SPACE helpmessagekey)* {pin=2}

private PermissionLine ::= permission separator PERMISSION_VALUE (DOT PERMISSION_VALUE)* {pin=2}

private UserLine ::= user separator (player | console | everyone) {pin=2}

private HookLine ::= hook separator hookname {pin=2}

private NoHelpLine ::= nohelp

private NoTabLine ::= notab

private PostArgLine ::= postarg

private ContextLine ::= context separator contextname (SPACE contextname)* {pin=2}

private AssertLine ::= assert separator assertname (SPACE assertname)* {pin=2}

private Open_Bracket ::= SPACE* OBRACKET newline
private Close_Bracket ::= SPACE* CBRACKET (newline|<<eof>>)

//Args_Consume ::= Arg*
private Args_Can_Consume ::= ConsumingArg*

private Args ::= Arg*

private Arg ::= (((ARG_TYPE CONSTRAINT? FLAG_COLON)? Flag) | (ARG_TYPE CONSTRAINT? COLON Argument)) (newline | SPACE)

private ConsumingArg ::= ARG_TYPE CONSTRAINT? CONSUME_TOKEN? COLON Consuming_Argument (newline | SPACE)

private Flag ::= FLAG_NAME (COMMA DASHES FLAG_NAME)* FLAG_MODIFIER? (BRACKET_OPEN DEFAULT_VALUE? BRACKET_CLOSE)?

private Argument ::= ARG_NAME (noshowtype | ((noshowtype) ( BRACKET_OPEN DEFAULT_VALUE? BRACKET_CLOSE)? ))?

private Consuming_Argument ::= ARG_NAME (noshowtype | ((notrequired | bothmodifiers) ( BRACKET_OPEN DEFAULT_VALUE? BRACKET_CLOSE)? ))?

//(CONSTRAINT_OPEN CONSTRAINT? CONSTRAINT_CLOSE)?